@group(3) @binding(0) var acc_struct: acceleration_structure;
struct RayTransmissionData { header: vec4<u32>, rows: array<vec4<f32>> };
@group(3) @binding(1) var<storage, read> ray_transmission: RayTransmissionData;
// Same visibility interface as shadows.wgsl. Both sampling and reconstruction
// use this implementation in RayTraced mode; screen depth never decides a hit.
fn shadow_factor_from_receiver(id: u32, origin: vec3<f32>, position: vec3<f32>, normal: vec3<f32>, pixel: vec2<f32>, frame: u32) -> Visibility {
    let light=lights[id];
    let cast_shadow=select(light.shadow_index!=INVALID_LIGHT,(light._pad&2u)!=0u,(light._pad&1u)!=0u);
    if !cast_shadow { return Visibility(1.0); }

    let inc=incident(light,origin);
    var distance=globals.ray_settings.x;
    if light.light_type!=0u { distance=length(light.position_range.xyz-origin)-0.0001; }
    if distance<=0.0001 { return Visibility(1.0); }
    var query: ray_query;
    if !USE_RAY_TRANSMISSION {
        // Preserve the opaque-only early-termination path.
        rayQueryInitialize(&query,acc_struct,RayDesc(0x05u,0xffu,0.0001,distance,origin,inc.direction));
        while rayQueryProceed(&query) {}
        return Visibility(select(0.0,1.0,rayQueryGetCommittedIntersection(&query).kind==RAY_QUERY_INTERSECTION_NONE));
    }
    // SceneDB classifies opaque BLAS geometry so hardware can terminate those
    // rays directly. Generic buffers may override BLAS opacity instead. Each
    // candidate triangle is a thin sheet; reject it after accumulating tint so
    // traversal keeps visiting candidates in its unspecified order.
    var throughput=vec3<f32>(1.0);
    rayQueryInitialize(&query,acc_struct,RayDesc(select(0x02u,0x04u,(ray_transmission.header.x&1u)!=0u),0xffu,0.0001,distance,origin,inc.direction));
    while rayQueryProceed(&query) {
        let hit=rayQueryGetCandidateIntersection(&query);
        if hit.instance_index>=min(ray_transmission.header.y,arrayLength(&ray_transmission.rows)) {
            rayQueryTerminate(&query);
            return Visibility(0.0);
        }
        throughput*=ray_transmission.rows[hit.instance_index].rgb;
        if all(throughput==vec3<f32>(0.0)) {
            rayQueryTerminate(&query);
            return visibility_from_rgb(throughput);
        }
    }
    if rayQueryGetCommittedIntersection(&query).kind!=RAY_QUERY_INTERSECTION_NONE { return Visibility(0.0); }
    return visibility_from_rgb(throughput);
}

fn shadow_factor(id: u32, position: vec3<f32>, normal: vec3<f32>, pixel: vec2<f32>, frame: u32) -> Visibility {
    return shadow_factor_from_receiver(id,shadow_receiver(position,normal,pixel),position,normal,pixel,frame);
}
